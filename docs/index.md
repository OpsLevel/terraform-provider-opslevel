---
page_title: "Provider: OpsLevel"
subcategory: ""
description: |-
  The OpsLevel provider provides lifecycle management of data in your OpsLevel account.
---

# OpsLevel Provider

Solve service ownership and ship more reliable services with our microservice catalog. Say goodbye to stale spreadsheets and break down knowledge silos for good.
Leverage Terraform to provide lifecycle management of the data in your OpsLevel account.

Use the navigation to the left to read about the available resources and datasources with detailed examples of how use them.

## Example Usage

```terraform
provider "opslevel" {
  api_token = "XXX" // or environment variable OPSLEVEL_API_TOKEN
}

resource "opslevel_team" "foo" {
  name = "foo"
  manager_email = "foo@example.com"
  responsibilities = "Responsible for foo frontend and backend"
}

resource "opslevel_service" "foo-frontend" {
  name = "foo-frontend"

  description = "The foo frontend service"
  framework   = "rails"
  language    = "ruby"

  lifecycle_alias = "beta"
  tier_alias = "tier_3"
  owner_alias = opslevel_team.foo.alias

  tags = [
    "environment:production",
  ]
}

data "opslevel_rubric_category" "security" {
  filter {
    field = "name"
    value = "Security"
  }
}

data "opslevel_rubric_level" "bronze" {
  filter {
    field = "name"
    value = "Bronze"
  }
}

resource "opslevel_filter" "filter" {
  name = "foo"
  predicate {
    key = "tier_index"
    type = "equals"
    value = "tier_3"
  }
  connective = "and"
}

resource "opslevel_check_repository_integrated" "foo" {
  name = "foo"
  enabled = true
  category = data.opslevel_rubric_category.security.id
  level = data.opslevel_rubric_level.bronze.id
  owner = opslevel_team.foo.id
  filter = opslevel_filter.filter.id
  notes = "Optional additional info on why this check is run or how to fix it"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `api_token` (String, Sensitive) The API authorization token. It can also be sourced from the OPSLEVEL_API_TOKEN environment variable.
- `api_url` (String) The url of the OpsLevel API to. It can also be sourced from the OPSLEVEL_API_URL environment variable.
- `client_timeout` (Int) Override value for the timeout to the graphql endpoint (defualt is 10s).

## Argument Reference

The following arguments are supported:

* `token` - (Required) The API authorization token. It can also be sourced from the OPSLEVEL_API_TOKEN environment variable.
