---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "opslevel_component_type Resource - terraform-provider-opslevel"
subcategory: ""
description: |-
  Component Type Definition Resource
---

# opslevel_component_type (Resource)

Component Type Definition Resource

## Example Usage

```terraform
resource "opslevel_component_type" "mobile" {
  name        = "Mobile App"
  alias       = "mobile-app"
  description = "A mobile app component type."
  icon = {
    color = "#f759ab"
    name  = "PhDeviceMobile"
  }
  properties = {
    os = {
      name                    = "Operation System"
      description             = "The operating system this app is deployed to."
      allowed_in_config_files = "true"
      locked_status           = "ui_locked"
      display_status          = "hidden"
      schema = jsonencode({
        "enum" : [
          "ios",
          "android",
          "both"
        ],
        "type" : "string"
      })
    }
    version = {
      name          = "Release Version"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "string",
        "pattern" : "^([0-9]+).([0-9]+).([0-9]+)"
      })
    }
    bundle-id = {
      name = "Bundle ID"
      schema = jsonencode({
        "type" : "string"
      })
    }
    update-strategy = {
      name          = "Update Strategy"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "code-push",
          "firebase",
          "liftoff"
        ],
        "type" : "string"
      })
    }
    crash-reporting = {
      name                    = "Crash Reporting"
      allowed_in_config_files = "true"
      locked_status           = "unlocked"
      schema = jsonencode({
        "enum" : [
          "sentry",
          "firebase",
          "crashlytics"
        ],
        "type" : "string"
      })
    }
  }
  relationships = {
    services = {
      name               = "Services Needed"
      description        = "The services this Mobile App depends on."
      allowed_categories = ["default"]
      allowed_types      = ["service"]
    }
  }
}

resource "opslevel_component_type" "lambda" {
  name        = "Lambda"
  alias       = "lambda"
  description = "A cloud function like AWS Lambda or Azure Functions."
  icon = {
    color = "#ffa940"
    name  = "PhFunction"
  }
  properties = {
    cloud = {
      name          = "Cloud Provider"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "aws",
          "azure",
          "gcp"
        ],
        "type" : "string"
      })
    }
    runtime = {
      name          = "Runtime"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "nodejs",
          "python",
          "java",
          "dotnet",
          "go",
          "ruby",
          "php"
        ],
        "type" : "string"
      })
    }
    release-strategy = {
      name          = "Release Strategy"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "manual",
          "automated",
        ],
        "type" : "string"
      })
    }
    concurrency = {
      name          = "Concurrency Limit"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "number"
      })
    }
    timeout = {
      name          = "Timeout"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "number"
      })
    }
    cold-start = {
      name          = "Cold Start Mitigation"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "boolean"
      })
    }
    dead-letter = {
      name          = "Dead Letter Queue"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "boolean"
      })
    }
  }
  relationships = {
    services = {
      name               = "Services Needed"
      description        = "The services this Lambda depends on."
      allowed_categories = ["default"]
    }
    libraries = {
      name          = "Libraries Needed"
      description   = "The libraries this Lambda depends on."
      allowed_types = ["service"]
    }
  }
}

resource "opslevel_component_type" "library" {
  name        = "Library"
  alias       = "library"
  description = "A non-runtime based component type that has a release strategy instead of continuous deployment."
  icon = {
    color = "#36cfc9"
    name  = "PhBooks"
  }
  properties = {
    release-strategy = {
      name          = "Release Strategy"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "manual",
          "automated",
        ],
        "type" : "string"
      })
    }
    artifact-repo = {
      name          = "Artifact Repository"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "array",
        "items" : {
          "enum" : [
            "maven",
            "npm",
            "nuget",
            "pypi",
            "gem",
            "docker",
            "homebrew",
            "github",
            "gitlab",
            "bitbucket",
            "aws",
            "azure",
            "gcp",
            "other"
          ],
          "type" : "string"
        },
        "minItems" : 1,
        "uniqueItems" : true
      })
    }
    version-strategy = {
      name          = "Version Strategy"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "semver",
          "calver",
          "custom"
        ],
        "type" : "string"
      })
    }
    cadence = {
      name          = "Cadence"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "weekly",
          "monthly",
          "quarterly",
          "yearly"
        ],
        "type" : "string"
      })
    }
    license = {
      name = "License"
      schema = jsonencode({
        "enum" : [
          "mit",
          "bsl",
          "apache",
          "other"
        ],
        "type" : "string"
      })
    }
    coverage = {
      name = "Test Coverage"
      schema = jsonencode({
        "type" : "number"
      })
    }
  }
}

resource "opslevel_component_type" "vendor" {
  name        = "Vendor Tool"
  alias       = "vendor"
  description = "A third party vendor tool."
  icon = {
    color = "#bae637"
    name  = "PhTruck"
  }
  properties = {
    vendor-contact = {
      name          = "Vendor Contact Name"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "string"
      })
    }
    vendor-contact-email = {
      name          = "Vendor Contact Email"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "string",
        "format" : "email"
      })
    }
    privacy-policy = {
      name          = "Privacy Policy"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "string"
      })
    }
    terms-of-service = {
      name          = "Terms of Service"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "string"
      })
    }
    status = {
      name          = "Status"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "Evaluating",
          "Purchasing",
          "Active",
          "Decommissioning",
          "Inactive"
        ],
        "type" : "string"
      })
    }
    business-unit = {
      name          = "Business Unit"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "Admin",
          "HR",
          "Customer Success",
          "Engineering",
          "Marketing",
          "Sales",
          "Legal",
          "Security",
          "Other"
        ]
        "type" : "string"
      })
    }
    risk = {
      name          = "Risk"
      locked_status = "unlocked"
      schema = jsonencode({
        "enum" : [
          "none",
          "low",
          "medium",
          "high"
        ],
        "type" : "string"
      })
    }
    pii = {
      name          = "Stores PII"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "boolean"
      })
    }
    subprocessor = {
      name          = "Subprocessor"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "boolean"
      })
    }
    contract-value = {
      name          = "Contract Value"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "number"
      })
    }
  }
}

resource "opslevel_component_type" "ml-ai" {
  name        = "ML / AI"
  alias       = "ml-ai"
  description = "A machine learning or ai model."
  icon = {
    color = "#737373"
    name  = "PhAtom"
  }
  properties = {
    contact = {
      name          = "Contact Name"
      locked_status = "unlocked"
      schema = jsonencode({
        "type" : "string"
      })
    }
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `alias` (String) The unique alias of the component type.
- `name` (String) The unique name of the component type.
- `properties` (Attributes Map) The properties of this component type. (see [below for nested schema](#nestedatt--properties))

### Optional

- `description` (String) The description of the component type.
- `icon` (Attributes) The icon associated with the component type (see [below for nested schema](#nestedatt--icon))
- `relationships` (Attributes Map) The relationships that can be defined for this component type. (see [below for nested schema](#nestedatt--relationships))

### Read-Only

- `id` (String) The ID of this resource.

<a id="nestedatt--properties"></a>
### Nested Schema for `properties`

Required:

- `name` (String) The name of the property definition.

Optional:

- `allowed_in_config_files` (Boolean) Whether or not the property is allowed to be set in opslevel.yml config files.
- `description` (String) The description of the property definition.
- `display_status` (String) The display status of the property.
- `locked_status` (String) Restricts what sources are able to assign values to this property.
- `schema` (String) The schema of the property.


<a id="nestedatt--icon"></a>
### Nested Schema for `icon`

Required:

- `color` (String) The color, represented as a hexcode, for the icon.
- `name` (String) The name of the icon in Phosphor icons for Vue, e.g. `PhBird`. See https://phosphoricons.com/ for a full list.


<a id="nestedatt--relationships"></a>
### Nested Schema for `relationships`

Required:

- `allowed_types` (List of String) The types of resources that can be selected for this relationship definition. Can include any component type alias on your account or 'team'.
- `name` (String) The display name of the relationship definition.

Optional:

- `description` (String) The description of the relationship definition.

Read-Only:

- `allowed_categories` (List of String) The categories of resources that can be selected for this relationship definition. Can include any component category alias on your account.

## Import

Import is supported using the following syntax:

```shell
terraform import opslevel_component_type.example Z2lkOi8vb3BzbGV2ZWwvU2VydmljZS82MDI0
```
